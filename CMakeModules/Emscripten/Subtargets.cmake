function(GET_SUBTARGET_OBJECTS VAR)# ARGN
	set(RESULT)
	foreach(SUBTARGET ${ARGN})
		set(TARGET_CHILDREN_EXPANDED)
		get_property(TARGET_CHILDREN TARGET ${SUBTARGET} PROPERTY OBJ_CHILDREN)
		if(TARGET_CHILDREN)
			GET_SUBTARGET_OBJECTS(TARGET_CHILDREN_EXPANDED ${TARGET_CHILDREN})
			list(APPEND RESULT ${TARGET_CHILDREN_EXPANDED})
		endif()
		list(APPEND RESULT $<TARGET_OBJECTS:${SUBTARGET}>)
	endforeach()
	set(${VAR} ${RESULT} PARENT_SCOPE)
endfunction()

function(LINK_SUBTARGET_DEPENDENCIES TARGT)# ARGN
	set(RESULT)
	foreach(SUBTARGET ${ARGN})
		get_property(TARGET_DEPENDENCIES TARGET ${SUBTARGET} PROPERTY OBJ_DEPENDENCIES)
		list(APPEND RESULT ${TARGET_DEPENDENCIES})
	endforeach()
	if(RESULT)
		list(REMOVE_DUPLICATES RESULT)
		target_link_libraries(${TARGT} ${RESULT})
		message(STATUS "Linking following libraries: ${RESULT}")
	endif()
endfunction()

function(INSTALL_SUBTARGET_HEADERS INSTALLPATH)# ARGN
	foreach(SUBTARGET ${ARGN})
		get_property(TARGET_HEADERS TARGET ${SUBTARGET} PROPERTY OBJ_INSTALL_HEADERS)
		get_property(TARGET_HEADER_LOCATION TARGET ${SUBTARGET} PROPERTY OBJ_INSTALL_LOCATION)
		get_property(TARGET_SOURCE_DIR TARGET ${SUBTARGET} PROPERTY OBJ_SOURCE_DIR)
		
		set(RESULT)
		foreach(HEADER ${TARGET_HEADERS})
			list(APPEND RESULT ${TARGET_SOURCE_DIR}/${HEADER})
		endforeach()
		install(FILES ${RESULT} DESTINATION ${INSTALLPATH}/${TARGET_HEADER_LOCATION})
	endforeach()
endfunction()